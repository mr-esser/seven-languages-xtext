/*******************************************************************************
 * Copyright (c) 2012 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 ******************************************************************************/
module org.xtext.mongobeans.GenerateMongoBeans

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.xtext.mongobeans"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				genericIde = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext\n */"
			}
		}
		language = StandardLanguage {
			name = "org.xtext.mongobeans.MongoBeans"
			fileExtensions = "mongo"

			serializer = {
				generateStub = false
			}
			scopeProvider = {
				generateStub = false
			}
			contentAssist = {
				generateStub = false
			}	
			junitSupport = {
				generateStub = false
			}
		}
	}
}
